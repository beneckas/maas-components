plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.trafi.example"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_BASE_URL", "\"${apiBaseUrl()}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion kotlin_version
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        // conflict between :ui and :routes:ui module both specifying e.g. ui_debug.kotlin_module
        // https://discuss.kotlinlang.org/t/dealing-with-kotlin-module-conflict-when-building-apk-for-project-with-non-unique-project-names/11247
        merge "/META-INF/*.kotlin_module"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
    }
}

androidExtensions {
    features = ["parcelize"]
}

dependencies {
    implementation project(":core")
    implementation project(":routes")
    implementation project(":routes:ui")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.ui:ui-tooling:$compose_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
}

String apiBaseUrl() {
    Properties localProperties = new Properties()
    rootProject.file("local.properties").withReader { localProperties.load(it) }

    def envVar = "TRAFI_API_BASE_URL"
    def localProp = "trafi.apiBaseUrl"
    def apiBaseUrl = System.hasProperty(envVar) ? System.getenv(envVar) : localProperties.getProperty(localProp)
    return apiBaseUrl ?: ""
}
